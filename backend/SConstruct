# -*- python -*-
#
# GINI Version 2.0
# (C) Copyright 2009, McGill University
#
# Scons compile script for creating GINI installation
#
import os
import SconsBuilder

EnsurePythonVersion(2,5)

# add our command line options
SconsBuilder.addCommandLineOptions()


try:
    Import('gini_home')
except:
    try:
        gini_home = os.environ['GINI_HOME']
    except KeyError:
        print "ERROR! The GINI_HOME environment variable not set."
        print "Set GINI_HOME and rerun the installation script."
        Exit(1)
    Export('gini_home')

gini_src = os.getcwd()
Export('gini_src')

try:
    Import('env')
except:
    env = Environment()
    conf = Configure(env)
    if not conf.CheckLib('slack'):
        print 'Did not find libslack.a or slack.lib, exiting!'
        Exit(1)
    if not conf.CheckLib('readline'):
        print 'Did not find libreadline.so or readline.lib, exiting!'
        Exit(1)
    if not conf.CheckLib('pthread'):
        print 'Did not find libpthread.so or pthread.lib, exiting!'
        Exit(1)
    env = conf.Finish()

all_files = Split ("""
                   include
                   fs
                   kernel
                   SconsBuilder.py
                   SConstruct
                   """)

Execute(Mkdir(gini_home + "/tmp"))
Execute(Mkdir(gini_home + "/sav"))
Execute(Mkdir(gini_home + "/etc"))
env.Alias('install', gini_home)

SConscript('src/grouter/SConscript', variant_dir='build/release/grouter', duplicate=0)
SConscript('src/uswitch/SConscript', variant_dir='build/release/uswitch', duplicate=0)
SConscript('src/wgini/SConscript', variant_dir='build/release/wgini', duplicate=0)

SConscript('src/gloader/SConscript')

SConscript('kernel/SConscript')
SConscript('fs/SConscript')

